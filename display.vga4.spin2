'Display driver for raycaster
'Rev.2A:  modified for P2 Rev.B
CON 'graphics constants 
    'BmpAddressBackground=$10000  '640x100 =65,080 bytes = $FE38 Bitmap with background
    'BmpAddressBrick=$20000  '5,176 bytes, $1438
    'BmpAddressFloor=$22000  '5,176 bytes, $1438
    'BmpAddressBrickFar=$24000  '5,176 bytes, $1438
    'BmpAddressBrickFarther=$26000  '5,176 bytes, $1438
    'TrigTables=$30000-$1000  '$10E24, 69,156 bytes
'    OffscreenBufferAddress=$40000   '320x240 =$12C00
'    DisplayBufferAddress=$50000+$3000 'size is 320x240=$12C00 (was 640x480=$4B0000)

CON  'VGA driver code starts, continues after cog starter  (note: modified for 320x240)

    intensity = 80    '0..128
    _clkfreq = 250_000_000
    fclk      = _clkfreq'_CLOCKFREQ '80_000_000.0
    fpix      = 25_000_000.0/2.0 'RJA trying for 320x240
    fset      = (fpix / fclk * 2.0) * float($4000_0000)
    vsync     =   4   'vsync pin 'RJA:  changed for real P2
  

VAR

    long DisplayBufferAddress
    long _params[6]

PUB Start(framebuffer_addr, pinbase, pPalette)

    pPal:=pPalette'long[pPalette]
    _params[0] := framebuffer_addr
    _params[1] := pinbase
    _params[2] := pinbase+1
    _params[3] := pinbase+2
    _params[4] := pinbase+3
    _params[5] := pinbase+4

    cognew(@DisplayEntry, @_params)

DAT     
orgh
org 0  'DisplayEntry
DisplayEntry
'
'
' Setup
'

'        add pPal,#$36    
        rdfast  #0,pPal'##@BmpAddressBackground-$400+$436     'load .bmp palette into lut
        'rdfast  #0,##@BmpAddressBackground-$400+$436 
        rep @.end,#$100
        rflong  y
        shl y,#8
        wrlut   y,x
        add x,#1
.end
        'RJA:  Now, repeating every line to stretch in y direction
        'rdfast  ##640*1/64/2,m_buf'##DisplayBufferAddress   'RJA:1 line of bitmapset rdfast to wrap on bitmap  //RJA making 320x480 (for now)

framebuff_addr
        rdlong  framebuff_addr, ptra++
hsync_pin
        rdlong  hsync_pin, ptra++
red_pin
        rdlong  red_pin, ptra++
green_pin
        rdlong  green_pin, ptra++
blue_pin
        rdlong  blue_pin, ptra++
vsync_pin
        rdlong  vsync_pin, ptra++

        setxfrq ##round(fset)       'set transfer frequency to 25MHz

        'the next 4 lines may be commented out to bypass level scaling

        setcy   ##intensity << 24   'r  set colorspace for rgb
        setci   ##intensity << 16   'g
        setcq   ##intensity << 08   'b
        setcmod #%01_0_000_0        'enable colorspace conversion
 
        'RJA dacmodes changed for real P2
        cogid   mycogid
        shl     mycogid, #8
        or  dacmode_s, mycogid
        or  dacmode_c, mycogid 

        'RJA dacmodes changed for real P2
        wrpin   dacmode_s, hsync_pin      'enable dac modes in pins 0..3
        wrpin   dacmode_c, red_pin
        wrpin   dacmode_c, green_pin
        wrpin   dacmode_c, blue_pin
        setnib  dira,#$f,#0 'RJA:  New for real P2

'
'
' Field loop
'
field       mov x,#33           'top blanks
        call    #blank
        mov     m_buf, #framebuff_addr
        mov     x,#480/2          'set visible lines
line        call    #hsync          'do horizontal sync
        rdfast  ##640*1/64/2,m_buf
        xcont   m_rf,#0         'visible line
        call    #hsync          'do horizontal sync
        xcont   m_rf,#0         'visible line
        add     m_buf,#320
        djnz    x,#line             'another line?
        
'        cogatn  #%100 'graphics cog is #2

        mov x,#10           'bottom blanks
        call    #blank

        drvnot  vsync_pin'#vsync          'sync on

        mov x,#2            'sync blanks
        call    #blank

        drvnot  vsync_pin'#vsync          'sync off

                jmp     #field                  'loop
'
'
' Subroutines
'
blank       call    #hsync          'blank lines
        xcont   m_vi,#0
    _ret_   djnz    x,#blank

hsync       xcont   m_bs,#0         'horizontal sync
        xcont   m_sn,#1
    _ret_   xcont   m_bv,#0
'
'
' Initialized data
'
'RJA:  New dacmodes for real P2
dacmode_s       long    %0000_0000_000_1011000000000_01_00000_0         'hsync is 123-ohm, 3.3V
dacmode_c       long    %0000_0000_000_1011100000000_01_00000_0         'R/G/B are 75-ohm, 2.0V

m_bs        long    $7F010000+16/2        'before sync
m_sn        long    $7F010000+96/2        'sync
m_bv        long    $7F010000+48/2        'before visible
m_vi        long    $7F010000+640/2       'visible

m_rf        long    $7F080000+640/2       'visible rlong 8bpp lut

x       long    0
y       long    0
m_buf       long    0
pPal        long    0
mycogid     long    0
'
'    
'BmpAddressBackground        
        'orgh    BmpAddressBackground - $436    'justify pixels at $1000, pallete at $1000-$400
        'file    "bgr_combopal2.bmp"   '640 x 100, 8pbb-lut  '65,080 bytes, 64,000 for data, $438 for other





